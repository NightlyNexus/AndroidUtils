apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

version = "2.0.0"
group = "com.uwetrottmann"

dependencies {
    compile 'com.android.support:support-v4:22.1.1'
}

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 22
        versionCode 2
        versionName "2.0.0"
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
}

configurations {
    archives {
        extendsFrom configurations.default
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonatypeRepo) {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
            name 'AndroidUtils'
            packaging 'aar'
            description 'Little helpers for Android development.'
            url 'https://github.com/UweTrottmann/AndroidUtils'

            scm {
                url 'scm:git@github.com:UweTrottmann/AndroidUtils.git'
                connection 'scm:git@github.com:UweTrottmann/AndroidUtils.git'
                developerConnection 'scm:git@github.com:UweTrottmann/AndroidUtils.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id "uwetrottmann"
                    name "Uwe Trottmann"
                    email "uwe.trottmann@gmail.com"
                }
            }
        }
    }
}
